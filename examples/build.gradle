plugins {
    id "com.athaydes.osgi-run" version "1.6.0"
}

dependencies {

    implementation project(':org.jgrapes.http')
    implementation project(':org.jgrapes.util')
    implementation project(':org.jgrapes.mail')
	implementation 'org.osgi:osgi.core:6.0.0'
	
    runtimeOnly 'org.apache.felix:org.apache.felix.framework:6.0.5'
    runtimeOnly ('org.apache.aries.spifly:org.apache.aries.spifly.dynamic.bundle:1.3.4') {
        transitive = true }
    runtimeOnly (project(':examples')) { transitive = true }
}

// For unknwown reasons required to prevent deadlock
jar {
    dependsOn ':org.jgrapes.core:generatePomFileForMavenPublication'
    dependsOn ':org.jgrapes.util:generatePomFileForMavenPublication'
    dependsOn ':org.jgrapes.io:generatePomFileForMavenPublication'
}

task runGreeter(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'org.jgrapes.examples.core.helloworld.Greeter'
}

task runEchoUntilQuit(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'org.jgrapes.examples.io.consoleinput.EchoUntilQuit'
    standardInput = System.in
}

task runEchoServer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'org.jgrapes.examples.io.tcpecho.EchoServer'
    standardInput = System.in
}

import aQute.bnd.gradle.Bndrun

task deb {
    doFirst {
        println "Classpath ${project.sourceSets.main.runtimeClasspath.files}"
    }
}

task runOSGiDemo(type: Bndrun) {
    group = 'verification'
    workingDirectory = project.file('.')
    bndrun = file("examples.bndrun")
    bundles = project.sourceSets.main.runtimeClasspath
}

eclipse {
    project {
       natures += 'bndtools.core.bndnature'
    }
    jdt {
        javaRuntimeName = "JavaSE-17"
    }
}

tasks.eclipse.dependsOn(cleanEclipse)